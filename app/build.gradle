plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "com.google.dagger.hilt.android"
//    id "com.google.gms.google-services" enable only when having google-services.json for firebase
}

android {
    def appId = "com.wagarcdev.der"
    def minApi = 23
    def targetApi = 33
    def appVersionCode = 1
    def appVersionName = "1.0.0"

    namespace appId
    compileSdk targetApi

    defaultConfig {
        applicationId appId
        minSdk minApi
        targetSdk targetApi
        versionCode appVersionCode
        versionName appVersionName
        vectorDrawables.useSupportLibrary true
    }

    signingConfigs {
        def signingProperties = new Properties()
        signingProperties.load(new FileInputStream(rootProject.file("signing.properties")))

        debug {
            storeFile = file("$rootDir/${signingProperties['DEBUG_KEYSTORE_FILE_PATH']}")
            storePassword signingProperties["DEBUG_KEYSTORE_PASSWORD"]
            keyAlias signingProperties["DEBUG_KEY_ALIAS"]
            keyPassword signingProperties["DEBUG_KEY_PASSWORD"]
        }

        release { // using debug key temporary while we don't have a release keystore
            storeFile = file("$rootDir/${signingProperties['DEBUG_KEYSTORE_FILE_PATH']}")
            storePassword signingProperties["DEBUG_KEYSTORE_PASSWORD"]
            keyAlias signingProperties["DEBUG_KEY_ALIAS"]
            keyPassword signingProperties["DEBUG_KEY_PASSWORD"]
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
            debuggable true
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion composeCompilerVer
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.activity:activity-compose:1.6.1"

    implementation "androidx.compose.animation:animation:$composeVer"
    implementation "androidx.compose.foundation:foundation:$composeVer"
    implementation "androidx.compose.runtime:runtime:$composeVer"
    implementation "androidx.compose.ui:ui:$composeVer"

    implementation "androidx.compose.ui:ui-tooling-preview:$composeVer"
    debugImplementation "androidx.compose.ui:ui-tooling:$composeVer"

    implementation "androidx.compose.material:material:$composeVer"
    implementation "androidx.compose.material:material-icons-extended:$composeVer"

    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanistVer"
    implementation "com.google.accompanist:accompanist-permissions:$accompanistVer"
    implementation "com.google.accompanist:accompanist-pager:$accompanistVer"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanistVer"

    implementation "com.google.dagger:hilt-android:$hiltVer"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVer"

    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    implementation "androidx.core:core-splashscreen:1.0.0"

    implementation "io.coil-kt:coil-compose:2.2.2"

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVer"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycleVer"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycleVer"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVer"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutinesVer"

    implementation "com.google.android.gms:play-services-auth:20.3.0"

    implementation "androidx.room:room-runtime:$roomVer"
    implementation "androidx.room:room-ktx:$roomVer"
    kapt "androidx.room:room-compiler:$roomVer"

    implementation platform("com.google.firebase:firebase-bom:31.0.2")
    implementation "com.google.firebase:firebase-firestore-ktx"
    implementation "com.google.firebase:firebase-storage-ktx"

}